# 获取某列最后一个包含数据的单元格所在行的行号或值

要获取某列最后一个包含数据的单元格所在行的行号，可以用下面的几个数组公式  
（注意是数组哦，Shift+Ctrl+Enter），以A列为例：

### 方法一：
`=MAX(NOT(ISBLANK(A1:A65535))*ROW(1:65535)) `   
公式输入完毕按 `Ctrl+Shift+Enter` 结束（下同），  
该公式对 `A` 列中每个包含数据的单元格指定其行号，对空单元格返回“0”，并用 `MAX` 函数得到最后一行的行号。

### 方法二：
`=LOOKUP(2,1/NOT(ISBLANK(A1:A65535)),ROW(A1:A65535)) `   
`“1/NOT(ISBLANK(A1:A65535))”` 返回一个由“1”和错误值组成的数组（false=0，true=1，1除以1等于1，1除以0当然是错误喽），其中非空单元格返回“1”，空单元格返回错误，LOOKUP函数的第一个参数为“2”，由于在上述数组中找不到，会返回第三个参数 `“ROW(A1:A65535)”` 中最后一个“1”对应的值，即最后一行的行号。

### 方法三：
`=MATCH(2,1/NOT(ISBLANK(A1:A65535))) `   
`MATCH` 函数的第三参数省略，将查找小于或等于“2”的最大值，返回最后一个“1”的位置，即最后一行的行号。

### 方法四：
如果A列中包含数值，下面的数组公式返回最后一个数值所在的行号，而忽略最后一个数值后面的文本、错误值等：  
`=MATCH(9E+307,A:A)` 


### 方法五：
A列最后有值的单元格的行号  
`=MAX(IF((A:A)<>"",ROW(A:A)))`  
A列最后一个值等于“a”的单元格行号  
`=MAX(IF((A:A)="a",ROW(A:A)))`  
然后按Ctrl+Shift+Enter完成  
![image.png](https://cdn.nlark.com/yuque/0/2021/png/12492743/1614303288202-8f030db8-a3bf-408e-b290-e74b0bdbff2b.png#align=left&display=inline&height=427&margin=%5Bobject%20Object%5D&name=image.png&originHeight=427&originWidth=515&size=17538&status=done&style=none&width=515)

### 类似的方法还可以得到最后一行的值：


#### 方法一：
`=LOOKUP(2,1/(A1:A65536<>""),A1:A65536) ` 

#### 方法二：
`=LOOKUP(2,1/(NOT(ISBLANK(A1:A65536))),A1:A65536) ` 

#### 方法三：
`=OFFSET(A1,MAX((A1:A65536<>"")*ROW(A1:A65536))-1,) ` 

